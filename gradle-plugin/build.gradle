plugins {
    alias(libraries.plugins.kotlin.jvm)
    alias(libraries.plugins.plugin.publish)
}

group = "io.github.b7woreo"
version = findProperty("gradle.publish.version") ?: "snapshot"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    withJavadocJar()
    withSourcesJar()
}

gradlePlugin {
    website = "https://github.com/b7woreo/TraceX"
    vcsUrl = "https://github.com/b7woreo/TraceX"

    plugins {
        tracex {
            id = 'io.github.b7woreo.tracex'
            implementationClass = 'tracex.TracePlugin'
            displayName = 'TraceX'
            description = 'TraceX plugin for gradle'
            tags.set(['android', 'method trace'])
        }
    }
}

dependencies {
    implementation libraries.android.gradle.api
    implementation libraries.android.zipflinger
    implementation libraries.asm
    implementation libraries.kotlinx.coroutine.core
    implementation libraries.guava
    testImplementation libraries.kotlin.test
}
